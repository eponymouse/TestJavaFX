import java.time.Duration

plugins {
    id 'java-library'
}

javafx {
    version = '11'
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.graphics' ]
    configuration = 'testImplementation'
}

dependencies {
    testImplementation project(':testjavafx-core')
    testImplementation project(':testjavafx-junit4')
    testImplementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    // For testing with Monocle:
    testImplementation 'org.testfx:openjfx-monocle:jdk-12.0.1+2'
}

test {
    useJUnit()

    scanForTestClasses = false
    filter {
        includeTestsMatching "*Test*"
        // Don't want inner classes to be scanned:
        excludeTestsMatching "*\$*"
    }

    forkEvery = 1
    maxParallelForks = 1
    timeout = Duration.ofMinutes(10)

    if ("true".equals(project.findProperty("testWithMonocle"))) {
        jvmArgs += ['-Dtestjavafx.headless=true',
                    '-Dprism.order=sw',
                    '-Dprism.text=t2k',
                    '-Dglass.platform=Monocle',
                    '-Dmonocle.platform=Headless',
                    '-Dheadless.geometry=1920x1200-32']
    }
}
